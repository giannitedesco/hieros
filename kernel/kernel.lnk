ENTRY(_start)
KERNEL_LMA = 1M;
KERNEL_VMA = 0xffffffff80000000;
SECTIONS
{
	 . = KERNEL_LMA;

	 .bootstrap :
	 {
		obj/kernel/entry32.o (.text)
	 }

	 . += KERNEL_VMA;
	 .text : AT(ADDR(.text) - KERNEL_VMA)
	 {
		_code = .;
		*(EXCLUDE_FILE(*obj/loader/*.o) .text.startup)
		*(EXCLUDE_FILE(*obj/loader/*.o) .text)
		*(.rodata*)
		. = ALIGN(4096);
	 }

	.data : AT(ADDR(.data) - KERNEL_VMA)
	{
		_data = .;
		*(.data)
		. = ALIGN(4096);
	}

	.eh_frame : AT(ADDR(.eh_frame) - KERNEL_VMA)
	{
		 _ehframe = .;
		 *(.eh_frame)
		. = ALIGN(4096);
	}

	.bss : AT(ADDR(.bss) - KERNEL_VMA)
	{
		_bss = .;
		*(EXCLUDE_FILE(*obj/loader/*.o) .bss)

		*(COMMON)
		. = ALIGN(4096);
	}

	_kernel_end = .;

	. = ALIGN(4096);
	.bss.loader : AT(ADDR(.bss.loader) - KERNEL_VMA)
	{
		obj/kernel/entry32.o (.bss)
	}

	. = ALIGN(4096);
	_very_end = .;

	/DISCARD/ :
	{
		*(.comment)
	}
}
